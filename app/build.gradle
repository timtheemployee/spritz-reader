apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.wxxtfxrmx.spritzreader"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "spritzreader_${variant.buildType.name}_${versionName}.apk"
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SHARED_PREFERENCE_KEY', "\"SPRITZ_PREFERENCES_KEY\""
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

// third-party libs
ext {
    gsonVersion = '2.8.5'
    dagger2 = '2.26'
    coroutinesCore = '1.3.3'
    iText = '7.1.10'
}

dependencies {

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCore"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesCore"
    implementation "com.itextpdf:itext7-core:$iText"
    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "com.google.dagger:dagger:$dagger2"
    implementation "com.google.dagger:dagger-android-support:$dagger2"
    kapt "com.google.dagger:dagger-compiler:$dagger2"
    kapt "com.google.dagger:dagger-android-processor:$dagger2"
}

//support & android dependencies
ext {
    supportLib = '1.1.0'
    designLib = '1.2.0-alpha03'
    constraintLayout = '1.1.3'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$supportLib"
    implementation "androidx.core:core-ktx:$supportLib"
    implementation "com.google.android.material:material:$designLib"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"
}


//test dependencies
ext {
    junit = '4.12'
    androidJUnit = '1.1.1'
    espresso = '3.2.0'
    mockito = '2.8.9'
    coroutinesTest = '1.3.6'
}

dependencies {
    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesTest"
    androidTestImplementation "androidx.test.ext:junit:$androidJUnit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}
